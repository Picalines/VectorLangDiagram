// Welcome to VectorLang!
// example: electrostatics

external Q1 = 1;
external Q1pos = {3.5, 3.5};

external Q2 = -1;
external Q2pos = {6.5, 6.5};

external gridWidth = 10;

const step = 0.5;

def fieldComponent(vector pos, number q, vector qPos) -> vector = [
    val diff := pos - qPos;

    val r := diff.length;
    val n := diff.normalize();

    if (r > 0)
        n * (q / r.sqr())
    else
        {0, 0}
];

def field(vector pos) -> vector = [
    val E1 := fieldComponent(pos, Q1, Q1pos);
    val E2 := fieldComponent(pos, Q2, Q2pos);

    E1 + E2
];

def plotField(vector pos) -> void = if (pos.x <= gridWidth and pos.y <= gridWidth) [
    push();
    translate(pos);

    val pf := field(pos);

    if (pf.length > step) [
        fill(WHITE.blend(RED, (pf.length - step) / step));
        pf := pf.clampLength(0, step);
    ];

    plot(pf);
    pop();

    if (pos.x >= gridWidth)
        plotField(pos + {-pos.x, step})
    else
        plotField(pos + {step, 0});
];

def main() -> void = [
    plotField({0, 0});
];
